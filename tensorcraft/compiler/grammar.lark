start: tensorexp+

tensorexp: tensor "=" tensorop -> assign
        | tensor "+=" tensorop -> add_assign
        | tensor "-=" tensorop -> sub_assign
        | tensor "*=" tensorop -> prod_assign
        | tensor "/=" tensorop -> div_assign

tensorop: operand
        | tensorop "+" operand -> add
        | tensorop "-" operand -> sub
        | tensorop "*" operand -> prod
        | tensorop "/" operand -> div
        | tensorop "==" operand -> equal
        | tensorop "!=" operand -> nequal
        | tensorop "&&" operand -> bool_and
        | tensorop "||" operand -> bool_or
        | tensorop ">" operand -> gt
        | tensorop "<" operand -> lt
        | tensorop ">=" operand -> ge
        | tensorop "<=" operand -> le

operand: signed_tensor
         | signed_idxvar
         | SIGNED_NUMBER
         | "(" tensorop ")" -> parenthesis

signed_tensor: tensor -> pos_tensor
                | "-" tensor -> neg_tensor

tensor: TENSORVAR
        | TENSORVAR "[" multiidx "]"

TENSORVAR: /[A-Z][a-zA-Z0-9_]*/

multiidx: idx
        | multiidx "," idx

idx: NUMBER              -> scalar_idx
     | INDEXVAR          -> var_idx
     | "(" INDEXVAR+ ")" -> joined_idx

signed_idxvar: INDEXVAR        -> pos_indexvar
                | "-" INDEXVAR -> neg_indexvar
INDEXVAR : /[a-z]/

_NL: /(\r?\n)+\s*/

%import common.SIGNED_NUMBER
%import common.NUMBER
%import common.WS


COMMENT: /\s*/ "#" /[^\n]/*

%ignore WS
%ignore COMMENT
